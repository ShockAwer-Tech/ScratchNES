define-atomic evaluate sprites scanline: (N)
delete all of secondary OAM
delete all of evaluation line
delete all of secondary bitmaps
delete all of secondary Xt
repeat 32
	add "255" to secondary OAM
	add "-1" to evaluation line
	add "-1" to evaluation line
	add "-1" to evaluation line
	add "-1" to evaluation line
	add "-1" to evaluation line
	add "-1" to evaluation line
	add "-1" to evaluation line
	add "-1" to evaluation line
end
set evaluation n to 1
set evaluation slot to 1
repeat 64
	if evaluation slot < 32 and not N < item evaluation n of OAM and item evaluation n of OAM + 8 > N then
		set temp to item (evaluation n + 3) of OAM
		replace item temp of evaluation line with evaluation slot
		replace item temp + 1 of evaluation line with evaluation slot
		replace item temp + 2 of evaluation line with evaluation slot
		replace item temp + 3 of evaluation line with evaluation slot
		replace item temp + 4 of evaluation line with evaluation slot
		replace item temp + 5 of evaluation line with evaluation slot
		replace item temp + 6 of evaluation line with evaluation slot
		replace item temp + 7 of evaluation line with evaluation slot
		get pattern tile:  (item (evaluation n + 1) of OAM) scanline:  (mY - item evaluation n of OAM) table:  (PPU Sprite pattern table)
		add mask to secondary bitmaps
		add temp - 3 to secondary Xt
		change evaluation slot by 1
	end
	change evaluation n by 4
end
; "TODO: evaluate sprites in secondary OAM"
; "TODO: buggy sprite overflow flag"

define-atomic get pattern tile: (tile) scanline: (scanline) table: (table)
set mask to item (16 * tile + scanline + table + 1) of oCHR

when 1 key pressed
ask "Cual?" and wait
if answer = "format chr" then
	format chr
else
	if answer = "format prg" then
		format prg
	else
		if answer = "irom" then
			ask "PRG?" and wait
			set temp to answer
			ask "CHR?" and wait
			unpack ROM (temp) banks PRG-ROM and (answer) banks CHR-ROM
		end
	end
end

; "optimizes CHR-ROM"

define-atomic format chr
delete all of oCHR
set temp to 1
repeat length of CHR-ROM
	set BG: Plane 0 to item (1 + (join "0x" (item temp of CHR-ROM))) of bitmask
	set BG: Plane 1 to item (1 + (join "0x" (item (temp + 8) of CHR-ROM))) of bitmask
	set BG: Plane 0 to (join "1" (BG: Plane 0))
	set BG: Plane 1 to (join "1" (BG: Plane 1))
	add BG: Plane 0 + BG: Plane 1 * 2 to oCHR
	change temp by 1
end

when q key pressed
emulate frame

define-atomic VBlank
set PPU vblank to 1
if PPU generate NMI = 1 then
	interrupt: vector "0xFFFA"
end
repeat until cycles > 2380
	step CPU
end
set cycles to 0

define-atomic skip frame
; "TODO: sprite 0 hit etc?"
set mY to -1
repeat 224
	change mY by 1
	repeat until mX > 254
		step CPU
		change mX by 3 * cycles
		set cycles to 0
	end
	; "TODO: HBlank"
end
VBlank

define-atomic emulate frame
go to x: -128 y: 128
set mY to -1
set adjacent count to 0
set adjacent color to -1
repeat 224
	change mY by 1
	evaluate sprites scanline:  (mY)
	go to x: -128 y: y position - 1
	pen down
	set mX to 0
	repeat until mX > 254
		step CPU
		step CPU
		if PPU show bg = 1 or PPU show sprites = 1 then
			repeat cycles
				if mX mod 8 = 0 then
					read PPU memory @ (8192 + mX / 8 + 32 * floor of (mY / 8))
					set BG: Plane 0 to item (16 * M + PPU Background pattern table + mY mod 8 + 1) of oCHR
					read PPU memory @ (9152 + floor of (mX / 16) + 16 * floor of (mY / 16))
					set offset to 2 * <mX mod 16 > 7> + 4 * <mY mod 16 > 7>
					set BG: Palette to 8 * letter (offset + 1) of item (1 + M) of bitmask + 4 * letter (offset + 2) of item (1 + M) of bitmask + 1
				end
				if item (mX + 1) of evaluation line < 1 then
					set color to item (BG: Palette + letter (2 + mX mod 8) of BG: Plane 0) of Palette
				else
					set tmp to letter (mX - item item (mX + 1) of evaluation line of secondary Xt) of item item (mX + 1) of evaluation line of secondary bitmaps
					if tmp = 0 then
						set color to item (BG: Palette + letter (2 + mX mod 8) of BG: Plane 0) of Palette
					else
						set color to item (17 + tmp) of Palette
					end
				end
				change mX by 1
				if adjacent color = color then
					change adjacent count by 1
				else
					set pen color to (adjacent color)
					change x by adjacent count
					set adjacent count to 1
					set adjacent color to color
				end
			end
		else
			change mX by 3 * cycles
		end
		set cycles to 0
	end
	if adjacent count > 0 then
		set pen color to (adjacent color)
		change x by adjacent count
		set adjacent count to 0
	end
	pen up
end
VBlank

define-atomic initialize PPU
pen up
clear
set pen size to 1
delete all of OAM
repeat 256
	add "0" to OAM
end
delete all of Nametables
repeat 960
	add "0x24" to Nametables
end
repeat 2048 - 960
	add "0" to Nametables
end
delete all of Palette
repeat 1 + 4 * 4 + 4 * 4
	add "0" to Palette
end
hide

define-atomic read PPU register (N)
if N = 2 then
	set M to 128 * PPU vblank + 64 * PPU sprite 0 + 32 * PPU sprite overflow
	set PPU address latch to 0
	set PPU vblank to 0
else
	if N = 7 then
		read PPU memory
	end
end

; "TODO: use O(logN) lookup instead of O(N)"

define-atomic write PPU register (N) value: (V)
if N = 0 then
	set mask to item (1 + V) of bitmask
	set PPU base nametable address to 2 * letter 7 of mask + letter 8 of mask
	set PPU VRAM increment to letter 6 of mask * 31 + 1
	set PPU Sprite pattern table to letter 5 of mask * 4096
	set PPU Background pattern table to letter 4 of mask * 4096
	set PPU Sprite size to letter 3 of mask
	set PPU master slave select to letter 2 of mask
	set PPU generate NMI to letter 1 of mask
else
	if N = 1 then
		set mask to item (1 + V) of bitmask
		set PPU grayscale to letter 8 of mask
		set PPU show left8 bg to letter 7 of mask
		set PPU show left8 sprites to letter 6 of mask
		set PPU show bg to letter 5 of mask
		set PPU show sprites to letter 4 of mask
		set PPU emphasize blue to letter 3 of mask
		set PPU emphasize green to letter 2 of mask
		set PPU emphasize red to letter 1 of mask
	else
		if N = 3 then
			set PPU OAMADDR to V
		else
			if N = 4 then
				replace item PPU OAMADDR mod 256 + 1 of OAM with V
				change PPU OAMADDR by 1
			else
				if N = 5 then
					if PPU address latch = 0 then
						set PPU fine x scroll to V
					else
						set PPU fine y scroll to V
					end
				else
					if N = 6 then
						if PPU address latch = 0 then
							; "TODO: is this correct?"
							set PPU VRAM address to V * 256
							set PPU address latch to 1
						else
							change PPU VRAM address by V
							set PPU address latch to 0
						end
					else
						if N = 7 then
							write PPU memory (V)
							change PPU VRAM address by 1
						end
					end
				end
			end
		end
	end
end

define-atomic OAM DMA (pagebase)
set temp to 0
repeat 256
	mapper read (256 * pagebase + PPU OAMADDR + temp)
	change temp by 1
	replace item temp of OAM with M
end
change cycles by 513

define-atomic read PPU memory
if PPU VRAM address < 32 * 256 then
	set M to item (1 + PPU VRAM address) of CHR-ROM
else
	if PPU VRAM address < 48 * 256 then
		set M to item (PPU VRAM address - 8191) of Nametables
	else
		if PPU VRAM address < 63 * 256 then
			set M to item (PPU VRAM address - 12287) of Nametables
		else
			if PPU VRAM address < 64 * 256 then
				set M to item (PPU VRAM address mod 32 + 1) of Palette
			else
				; "TODO: PPU memory mirroring"
			end
		end
	end
end

define-atomic read PPU memory @ [VRAM address]
if VRAM address < 32 * 256 then
	set M to item (1 + VRAM address) of CHR-ROM
else
	if PPU VRAM address < 48 * 256 then
		set M to item (VRAM address - 8191) of Nametables
	else
		if PPU VRAM address < 63 * 256 then
			set M to item (VRAM address - 12287) of Nametables
		else
			if PPU VRAM address < 64 * 256 then
				set M to item (VRAM address mod 32 + 1) of Palette
			else
				; "TODO: PPU memory mirroring"
			end
		end
	end
end

define-atomic write PPU memory (V)
if PPU VRAM address < 32 * 256 then
	replace item 1 + PPU VRAM address of CHR-ROM with V
else
	if PPU VRAM address < 48 * 256 then
		replace item PPU VRAM address - 8191 of Nametables with V
	else
		if PPU VRAM address < 63 * 256 then
			replace item PPU VRAM address - 12287 of Nametables with V
		else
			if PPU VRAM address < 64 * 256 then
				replace item PPU VRAM address mod 32 + 1 of Palette with item (V + 1) of RGB
			else
				; "TODO: PPU memory mirroring"
			end
		end
	end
end
